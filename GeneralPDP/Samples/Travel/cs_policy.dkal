#include "common.dkal"

// Trust the decisions from the University
_me knows
  uni_dkal tdonS travel for POS to DEST on AMOUNT dollars is allowed
  uni_dkal tdonS travel for POS to DEST on AMOUNT dollars is not allowed

// If the same city was approved on the same or larger amount, approve
if _me knows
  req R issued by P
  req R by P has "resource" attribute string "destination" valued DEST
  req R by P has "resource" attribute int "grant-amount" valued RM
  req R by P has "subject" attribute string "position" valued POS
  travel for POS to DEST on AM dollars is allowed
  asInfon(RM <= AM)
then
  say with justification to P
    req R by P decision "Permit"

// If the same city was disapproved on the same or smaller amount, disapprove
if _me knows
  req R issued by P
  req R by P has "resource" attribute string "destination" valued DEST
  req R by P has "resource" attribute int "grant-amount" valued RM
  req R by P has "subject" attribute string "position" valued POS
  travel for POS to DEST on AM dollars is not allowed
  asInfon(RM >= AM)
then
  say with justification to P
    req R by P decision "Deny"

// If there is no knowledge, ask the university
if _me knows
  req R issued by P
  req R by P has "resource" attribute string "destination" valued DEST
  req R by P has "resource" attribute int "grant-amount" valued M
  req R by P has "subject" attribute string "position" valued POS
then
  say with justification to uni_dkal
    is travel grant for POS to DEST on M dollars allowed

	
