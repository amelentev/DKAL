{
module Microsoft.Research.Dkal.SimpleParser.Lexer

open System
open Microsoft.FSharp.Text.Lexing

open Parser

let lexeme lexbuf = LexBuffer<char>.LexemeString lexbuf
let stringBuf = new System.Text.StringBuilder()
let add (ch:char) = stringBuf.Append ch |> ignore
}

// These are some regular expression definitions
let digit = ['0'-'9']
let whitespace = [' ' '\t' ]
let newline = ('\n' | '\r' '\n')

rule tokenize = parse
// Whitespace
| whitespace		{ tokenize lexbuf }
| newline			{ tokenize lexbuf }

// Comments
| '/' '*'            { comment lexbuf }
| '/' '/' [^ '\n']*  { tokenize lexbuf }

// Binary operators
| "&&"				{ AND }
| "||"				{ OR }
| "->"				{ IMPLIES }
| "="				{ EQ }

// Unary operators
| "said"			{ SAID }
| "!"				{ NOT }

// Delimiters
| "("				{ LPAR }
| ")"				{ RPAR }
| ":"				{ COLON }
| ","				{ COMMA }
| "."				{ DOT }

// Type renames
| "type"			{ TYPE }

// Table declarations
| "table"			{ TABLE }

// Relation declarations
| "relation"		{ RELATION }

// Function declarations
| "function"		{ FUNCTION }

// Literals
| "true"			{ TRUE } 
| "false"			{ FALSE } 
| ['-']?digit+									{ INT_LITERAL (Int32.Parse(lexeme lexbuf)) }
| ['-']?digit+('.'digit+)?(['e''E']digit+)?		{ FLOAT_LITERAL (Double.Parse(lexeme lexbuf)) }
| '"'                { stringBuf.Length <- 0;
                       string lexbuf;
                       STRING_LITERAL (stringBuf.ToString()) }

// Identifiers
| ['a'-'z']+ ['a'-'z''A'-'Z']*		{ LOWER_IDENTIFIER (lexeme lexbuf) }
| ['A'-'Z']+ ['a'-'z''A'-'Z']*		{ UPPER_IDENTIFIER (lexeme lexbuf) }

// EOF
| eof   { EOF }

and comment = parse 
| "*/"               { tokenize lexbuf }
| eof                { EOF }
| _                  { comment lexbuf }

and string = parse
| '"' { () }
| '\\' 'n' { add '\n'; string lexbuf }
| '\\' 'r' { add '\r'; string lexbuf }
| '\\' 't' { add '\t'; string lexbuf }
| '\\' _ { add (lexbuf.Lexeme.[1]); string lexbuf }
| [^ '\\' '"']+ { stringBuf.Append (lexeme lexbuf) |> ignore; string lexbuf }
| _ { add (lexbuf.Lexeme.[0]); string lexbuf }
| eof { () }
