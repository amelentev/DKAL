// common declarations
type Principal = Dkal.Principal
type Int = System.Int32
type String = System.String
type Boolean = System.Boolean

type Subject = String

relation needsExamContents(PERSON: Principal, SUBJECT: Subject)
relation asksForExamContents(PERSON: Principal, SUBJECT: Subject)
relation examContents(SUBJECT: Subject, CONTENTS: String)

substrate xml("<subjects>
                    <subject name='calculus'>
                        <professor name='smith' />
                    </subject>
                    <subject name='programming' >
                        <professor name='smith' />
                        <professor name='suresh' />
                        <student name='david' />
                    </subject>
                </subjects>") 
    namespaces "subjects"

macro isProfessor(PROFESSOR: Principal) : Boolean
    {| "basic" | Ret := asBoolean({| "subjects" | "//professor/@name" | PROFESSOR |}) |}
//    {| "subjects" | "//professor/@name" | PROFESSOR |}

macro isSubject(SUBJECT: Subject) : Boolean
    {| "basic" | Ret := asBoolean({| "subjects" | "//subject/@name" | SUBJECT |}) |}
    
macro inChargeOf(SUBJECT: Subject, PROFESSOR: Principal) : Boolean
    {| "basic" | Ret := asBoolean({| "subjects" | "//subject[@name='$SUBJECT']/professor[@name='$PROFESSOR']" |}) |}
    
macro enlistedIn(SUBJECT: Subject, STUDENT: Principal) : Boolean
    {| "basic" | Ret := asBoolean({| "subjects" | "//subject[@name='$SUBJECT']/student[@name='$STUDENT']" |}) |}
    
---smith--------------------------
// prof. smith's policy

substrate xml("<exams>
                    <exam subject='calculus' contents='what is the derivative of e^x?' />
                    <exam subject='programming' contents='is quicksort better than mergesort?' />
                </exams>") 
    namespaces "exams"

with P: Principal, SUBJECT: Subject, CONTENTS: String
    upon
        P said asksForExamContents(P, SUBJECT)
    if
        asInfon(inChargeOf(SUBJECT, P)) &&
        asInfon({| "exams" | "//exam[@subject='$SUBJECT']/@contents" | CONTENTS |}) 
    do
        send to P:
            examContents(SUBJECT, CONTENTS)
        drop
            P said asksForExamContents(P, SUBJECT)
            
---suresh--------------------------
// prof. suresh's policy

do once
    learn needsExamContents(Me, "programming")
    
with PROF: Principal, SUBJECT: Subject, CONTENTS: String
    if 
        needsExamContents(Me, SUBJECT) && 
        asInfon(isProfessor(PROF)) &&
        asInfon(inChargeOf(SUBJECT, PROF)) &&
        asInfon(inChargeOf(SUBJECT, Me)) &&
        asInfon({| "basic" | PROF != Me |})
    do
        send to PROF:
            asksForExamContents(Me, SUBJECT)
        forget
            needsExamContents(Me, SUBJECT)
            
---david--------------------------
// david's policy

with PROF: Principal, SUBJECT: Subject
    if 
        asInfon(isSubject(SUBJECT)) &&
        asInfon(enlistedIn(SUBJECT, Me)) &&
        asInfon(isProfessor(PROF)) &&
        asInfon(inChargeOf(SUBJECT, PROF))
    do
        send to PROF:
            asksForExamContents(Me, SUBJECT)

            