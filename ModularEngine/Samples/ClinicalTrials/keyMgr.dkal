substrate sql("Server=localhost,1136;Database=ClinicalTrials;User ID=dkal;Password=dkal;Trusted_Connection=True;Encrypt=False;", "schema.dbml")
	namespaces "ct"
substrate xml("keys.xml")
	namespaces "keys"

type Int = System.Int32
type Boolean = System.Boolean
type String = System.String
type Trial = Int
type Record = Int
type Principal = Dkal.Principal

relation requestToRead(P: Principal, R: Record)
relation mayRead(PHYS: Principal, R: Int)
relation keyForRecord(K: Int, R: Record)

macro record (N: Int, T:Trial): Record
    {| "ct" | records.patient == N && 
                records.trial == T &&
                Ret == records.id |}

macro org(T: Trial) : String
    {| "ct" | T == trials.id && 
                Ret == trials.organizer |}

with PERSON: Principal, ORG: Principal, TRIAL: Trial, R: Record, K: Int, N: Int
	upon
		(PERSON said requestToRead(PERSON, R)) &&
		(PERSON said ORG said mayRead(PERSON, R))
	if
		asInfon({|"ct"|
			ppalName(ORG) == org(TRIAL) &&
			R == record(N, TRIAL)
		|})
	if
		asInfon({|"keys"|
			"/keys/key[@record='$R']/@key" | K
		|})
	do
		send to PERSON:
			keyForRecord(K, R)
            