substrate sql("Server=localhost,1136;Database=ClinicalTrials;User ID=dkal;Password=dkal;Trusted_Connection=True;Encrypt=False;", "schema.dbml")
	namespaces "ct"

type Int = System.Int32
type Boolean = System.Boolean
type String = System.String
type Trial = Int
type Record = Int
type Principal = Dkal.Principal

relation physParticipates(PHYS: Principal, T: Trial, SITE: Principal)
relation physAllocatedPatients(PHYS: Principal, P1: Int, P2: Int, T: Trial, SITE: Principal)
relation requestToRead(P: Principal, R: Record)
relation mayRead(P: Principal, R: Record)
relation keyForRecord(K: Int, R: Record)

macro needInfo(N: Int) : Boolean
    {| "ct" | physPatients.id == N && 
                Ret == physPatients.needInfo |}

macro record (N: Int, T:Trial): Record
    {| "ct" | records.patient == N && 
                records.trial == T &&
                Ret == records.id |}

macro org(T: Trial) : String
    {| "ct" | T == trials.id && 
                Ret == trials.organizer |}

with SITE: Principal, TRIAL: Trial, P1: Int, P2: Int
	upon SITE said physParticipates(Me, TRIAL, SITE)
	do learn physParticipates(Me, TRIAL, SITE)

	upon SITE said physAllocatedPatients(Me, P1, P2, TRIAL, SITE)
	do learn physAllocatedPatients(Me, P1, P2, TRIAL, SITE)

with K: Int, R: Int
	upon keyMgr said keyForRecord(K, R)
	do learn keyForRecord(K, R)

with SITE: Principal, TRIAL: Trial, P1: Int, P2: Int, N: Int, ORG: Principal, R: Int
	if
		physParticipates(Me, TRIAL, SITE) &&
		physAllocatedPatients(Me, P1, P2, TRIAL, SITE) &&
		asInfon({|"ct"|
			P1 <= N && N <= P2 &&
			needInfo(N) &&
			ppalName(ORG) == org(TRIAL) &&
			R == record(N, TRIAL)
		|})
	do
		send to keyMgr:
			requestToRead(Me, R)
        send to keyMgr:
			ORG said mayRead(Me, R)