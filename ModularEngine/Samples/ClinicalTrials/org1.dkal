substrate sql("Server=localhost,1136;Database=ClinicalTrials;User ID=dkal;Password=dkal;Trusted_Connection=True;Encrypt=False;MultipleActiveResultSets=True;", "schema.dbml")
	namespaces "ct"

type Int = System.Int32
type Boolean = System.Boolean
type String = System.String
type Trial = Int
type Record = Int
type Principal = Dkal.Principal
type Infon = Dkal.Infon
type Evidence = Dkal.Evidence

relation participates(SITE: Principal, T: Trial)
relation allocatedPatients(SITE: Principal, N1: Int, N2: Int, T: Trial)
relation mayRead(P: Principal, R: Record)

macro org(T: Trial) : String
    {| "ct" | T == trials.id && 
                Ret == trials.organizer |}

macro siteUnnotified(S: Principal, T: Trial) : Boolean
    {| "ct" | siteAssignments.site == ppalName(S) && 
                siteAssignments.trial == T && 
                Ret == siteAssignments.unnotified |}

macro sitePatients(S: Principal, T: Trial, N1: Int, N2: Int) : Boolean
    {| "ct" | siteAssignments.site == ppalName(S) && 
                siteAssignments.trial == T && 
                siteAssignments.n1 == N1 && 
                siteAssignments.n2 == N2 && 
                Ret == true |}

macro record (N: Int, T:Trial): Record
    {| "ct" | Ret == records.id && records.patient == N && records.trial == T |}

do once
	apply {| "ct" | true | update siteAssignments.unnotified := true |}

with TRIAL: Trial, N1: Int, N2: Int, SITE: Principal
	if
		asInfon({|"ct"|
			ppalName(Me) == org(TRIAL) &&
			siteUnnotified(SITE, TRIAL) &&
			sitePatients(SITE, TRIAL, N1, N2)
		|})
	do
		say with justification to SITE:
			participates(SITE, TRIAL) 
        say with justification to SITE:
            allocatedPatients(SITE, N1, N2, TRIAL)
		send with justification to SITE:
			with PERSON: Principal, R: Int, N: Int
                (SITE said mayRead(PERSON, R)) && asInfon({|"ct"|
                    N1 <= N && N <= N2
                    && R == record(N, TRIAL)
                |})
                ->
                Me said mayRead(PERSON, R)
		apply {|"ct"| siteAssignments.site == ppalName(SITE) && siteAssignments.trial == TRIAL | update siteAssignments.unnotified := false |}
