substrate sql("Server=localhost,1136;Database=edkaldemo;User ID=dkal;Password=dkal;Trusted_Connection=True;Encrypt=False;", "schema.dbml")
	namespaces "ct"

type Int = System.Int32
type Bool = System.Boolean
type Trial = Int
type Record = Int
type Principal = Dkal.Principal

relation participates(SITE: Principal, T: Trial)
relation allocatedPatients(SITE: Principal, N1: Int, N2: Int, T: Trial)

relation physParticipates(PHYS: Principal, T: Trial, SITE: Dkal.Principal)
relation physAllocatedPatients(PHYS: Principal, P1: Int, P2: Int, Trial: Int, SITE: Dkal.Principal)
relation mayRead(PHYS: Dkal.Principal, R: Int)

macro physUnnotified(P: Int, T: Trial) : Bool
{|"ct"|physAssignments.phys == P && physAssignments.trial == T && Ret == physAssignments.unnotified|}

macro physPatients(P: Int, T: Trial, N1: Int, N2: Int) : Bool
{|"ct"|physAssignments.phys == P && physAssignments.trial == T && physAssignments.n1 == N1 && physAssignments.n2 == N2 && Ret == true|}

macro ppalId(P: Principal) : Int
{|"ct"| Ret == dkal_principals.id && dkal_principals.name == ppalName(P)|}

macro record (N: Int, T:Trial): Record
{|"ct"| Ret == records.id && records.patient == N && records.trial == T |}

macro orgId(T: Trial) : Int
{|"ct"| T==trials.id && Ret==trials.cro |}


with TRIAL: Trial, N1: Int, N2: Int, ORG: Principal
	upon ORG said participates(Me, TRIAL) && allocatedPatients(Me, N1, N2, TRIAL) 
	if asInfon({|"ct"| orgId(TRIAL) == ppalId(ORG) |})
	do learn participates(Me, TRIAL) && allocatedPatients(Me, N1, N2, TRIAL)

with TRIAL: Trial, N1: Int, N2: Int, PID: Int, P1: Int, P2: Int, PHYS: Principal, N: Int, R: Record, ORG: Principal
	if
		participates(Me, TRIAL) && 
		allocatedPatients(Me, N1, N2, TRIAL) &&
		asInfon({|"ct"|
			physUnnotified(PID, TRIAL) && physPatients(PID, TRIAL, P1, P2)
			&& N1 <= P1 && P2 <= N2 
			&& ppalId(PHYS) == PID
		|})
	do
		say to PHYS:
			physParticipates(PHYS, TRIAL, Me) &&
			physAllocatedPatients(PHYS, P1, P2, TRIAL, Me)
		send to PHYS:
			asInfon({|"ct"|
				P1 <= N && N <= P2 
				&& R == record(N,TRIAL)
			|})
			->
			Me said mayRead(PHYS, R)
		send to PHYS:
			Me said mayRead(PHYS, R)
			&& asInfon({|"ct"|
				P1 <= N && N <= P2 
				&& R == record(N,TRIAL)
				&& ppalId(ORG) == orgId(TRIAL)
			|})
			->
			ORG said mayRead(PHYS, R)