/// Airline check-in
/// The Cautious Airline (ca) requests that travellers present a passport-issuance
/// certificate on checkin. John is about to travel with ca, and therefore requests 
/// his Passport Agency (pa1) to give him a certificate. This certificate is forwarded 
/// to ca so that they can issue John's boarding pass.

// common declarations

// type renames
type Principal = Dkal.Principal
type Infon = Dkal.Infon
type Evidence = Dkal.Evidence
type Int = System.Int32
type Passport = Int
type Flight = Int
type Seat = Int

// P needs an issuance certificate for his passport
relation passportCertificateNeeded(P: Principal)

// P has passport number N
relation issuedPassport(P: Principal, N: Passport)

// P requests check-in on flight F
relation checkinNeeded(P: Principal, F: Flight)

// P has boarding pass with seat S assigned on flight F
relation boardingPass(P: Principal, F: Flight, S: Seat)

---john--------------------------
// john's policy

// datasource with itineraries for john
datasource xml("<itineraries>
                    <itinerary flight='984' to='SEA' from='EZE' airline='ca' />
                </itineraries>") 
    namespaces "itineraries"

// to keep track that john does not have issuance certificate or boarding passes
relation noCertificate()
relation noBoardingPass(AIRLINE: Principal, F: Flight)

do once
    learn noCertificate()

with AIRLINE: Principal, F: Flight
    if
        asInfon({| "itineraries" | "//itinerary" | AIRLINE <-> "airline", F <-> "flight" |})
    do once
        learn noBoardingPass(AIRLINE, F)
    
// whenever john is about to travel using cautious airline (ca), if he does not
// have an passport-issuance certificate, he first gets one from his issuing 
// passport agency (pa1)
if
    asInfon({| "itineraries" | "//itinerary[@airline='ca']" |}) && 
    noCertificate()
do
    say with justification to pa1:
        passportCertificateNeeded(Me)
    forget
        noCertificate()
            
// once john has his issuance certificate from the passport agency, then he can
// proceed to perform checkin, by forwarding his certificate to the cautious 
// airline
with F: Flight, N: Passport, E: Evidence
    upon
        pa1 said issuedPassport(Me, N) [E]
    if
        asInfon({| "itineraries" | "//itinerary[@airline='ca']/@flight" | F |}) &&
        noBoardingPass(ca, F)
    do
        say with justification to ca:
            checkinNeeded(Me, F)
        send to ca:
            pa1 said issuedPassport(Me, N) [E]
        forget
            noBoardingPass(ca, F)
    
---pa1--------------------------
// passport agency 1's policy

// datasource with passports issued by this agency
datasource xml("<passports>
                    <passport number='948823' name='john' />
                    <passport number='924875' name='jane' />
                </passports>") 
    namespaces "passports"
    
// if a principal asks a passport certificate, and pa1 has it store, it will 
// honor the request
with P: Principal, N: Passport
    upon 
        P said passportCertificateNeeded(P) [_]
    if
        asInfon({| "passports" | "//passport[@name='$P']/@number" | N |})
    do
        say with justification to P:
            issuedPassport(P, N)
            
---ca--------------------------
// cautious airline's policy

// datasource to keep track of trusted passport agencies
datasource xml("<agencies>
                    <agency name='pa1' />
                    <agency name='pa23' />
                </agencies>") 
    namespaces "agencies"

// datasource to keep track of flights
datasource xml("<flights>
                    <flight number='984'>
                        <passenger name='john' seat='34' />
                        <passenger name='carl' seat='20' />
                    </flight>
                </flights>") 
    namespaces "flights"

with PA: Principal, I: Infon, E: Evidence
	upon
		PA said I [E]
    if
        asInfon({| "agencies" | "//agency[@name='$PA']" |})
	do
		learn PA said I [E]

with P: Principal, F: Flight, E: Evidence
	upon 
		P said checkinNeeded(P, F) [E]
	do
		learn
			P said checkinNeeded(P, F) [E]
		
with P: Principal, F: Flight, S: Seat, PA: Principal, N: Passport
    if 
        P said checkinNeeded(P, F) [_]
    if
        PA said issuedPassport(P, N) [_]
    if
        asInfon({| "flights" | "//flight[@number='$F']/passenger[@name='$P']/@seat" | S |})
    do
        say with justification to P:
            boardingPass(P, F, S)
    
    
    