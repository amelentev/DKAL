/// Ping-pong
/// Two principals, ping and pong, send a number to each other in the ping-pong fashion.
/// Each time they double the number, until the limit of 100000 is reached.

// common
type Int = System.Int32

// an infon relation to hold the number that is passed around
relation number(I: Int)

---ping-----------------------------
// ping's policy

// start the chain reaction
knows number(1)

if number(1)
do
    send to pong: number(1)
    forget number(1)


// reply to pong's messages
with X: Int, Y: Int
    upon
        // whenever a message from pong arrives
        pong said number(X)
    if
        // check that the number is less than 100000 and if so double it
        asInfon({|"basic"|X < 100000|}) &&
        asInfon({|"basic"|Y:=X*2|})
    do
        // send that new number back to pong
        send to pong:
            number(Y)

---pong-----------------------------
// pong's policy

// reply to ping's messages
with X: Int, Y: Int
    upon
        ping said number(X)
    if
        // check that the number is less than 100000 and if so double it
        asInfon({|"basic"|X < 100000|}) &&
        asInfon({|"basic"|Y:=X*2|})
    do
        // send that new number back to ping
        send to ping:
            number(Y)
