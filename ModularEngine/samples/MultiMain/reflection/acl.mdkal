datasource xml("<root>
	<groups>
		<g1><p1/></g1>
		<g3><p3/></g3>
	</groups>
</root>") namespaces "x"
datasource reflection("ACL, ACL") namespaces "r"

type String = System.String
type Principal = String
type Group = String
type Object = String

relation permitg(G: Group, O: Object)
relation permit(P: Group, O: Object)

relation askpermit(O: Object)
relation log(S: String)

// permitg definition:
knows forall O: Object, G: Group
	{|r| G = owns(O) |} -> permitg(G, O)
knows forall O: Object, O1: Object, G: Group
	{|r| notOwned(O) |} && {|r| O1 = getParent(O) |} && permitg(G, O1) -> permitg(G, O)
	
// permit definition:
knows forall O: Object, G: Group, P: Principal
	permitg(G, O) && {|x| "//groups/$G/*" | P |} -> permit(P, O)

---p1---

// queries:
knows askpermit("i1") //g1,p1
knows askpermit("i1/i2") //g1,p1
knows askpermit("i1/i2/i3") //g3,p3

with O: Object, G: Group, P: Principal

if	askpermit(O) && permitg(G, O)
do	send to me: permitg(G, O)
	
if	askpermit(O) && permit(P, O)
do	send to me: permit(P, O)

if	permit("p1", "i1/i2")
do	send to me: log("ok")

if	permit("p3", "i1/i2")
do	send to me: log("error")

// todo:
if	permit("p1", O)
do	send to me: permit("p1 has access to:", O)
