/// This example models rumors among principals

relation isTheNextBigStar(P: Dkal.Principal)

---alice-----------------------------
// alice starts a rumor about her being the next big star
do once
    send to bob: isTheNextBigStar(me)
    send to chuck: isTheNextBigStar(me)

---bob-----------------------------
// bob tells any rumors about next big stars to his friend chuck
with P: Dkal.Principal, Q: Dkal.Principal
    upon 
        P said isTheNextBigStar(Q)
    do
        send to chuck: isTheNextBigStar(Q)

---chuck-----------------------------
// chuck learns everything he hears
with P: Dkal.Principal, I: Dkal.Infon
    upon 
        P said I
	do
		learn P said I

// chuck is a good journalist and he needs two different sources
// before he accepts something as valid
with P: Dkal.Principal, Q: Dkal.Principal, R: Dkal.Principal
    do once
        learn 
			(Q said isTheNextBigStar(P)) &&
			(R said isTheNextBigStar(P)) &&
			asInfon({| "basic" | Q != R |}) -> 
			isTheNextBigStar(P)

// if chuck knows about a coming big star it will publish it
with P: Dkal.Principal
    if
        isTheNextBigStar(P)
    do
        send to press: isTheNextBigStar(P)
        
---press-----------------------------
// press is a principal with no policy
