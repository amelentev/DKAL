datasource sql("Server=localhost,1136;Database=ClinicalTrials;User ID=dkal;Password=dkal;Trusted_Connection=True;Encrypt=False;MultipleActiveResultSets=True;", "schema.dbml")
	namespaces "ct"
datasource xml("keys.xml")
	namespaces "keys"

type Int = System.Int32
type Boolean = System.Boolean
type String = System.String
type Trial = Int
type Record = Int
type Principal = Dkal.Principal
type Infon = Dkal.Infon
type Evidence = Dkal.Evidence

relation requestToRead(P: Principal, R: Record)
relation mayRead(PHYS: Principal, R: Int)
relation keyForRecord(K: Int, R: Record)

macro record (N: Int, T:Trial): Record
    {| "ct" | records.patient == N && 
                records.trial == T &&
                Ret == records.id |}

macro org(T: Trial) : String
    {| "ct" | T == trials.id && 
                Ret == trials.organizer |}

with X: Infon, P: Principal, E: Evidence
    upon
        P said X [E]
    do
        learn P said X [E]
with I1: Infon, I2: Infon, P: Principal, E: Evidence
    upon
        I1 -> P said I2 [E]
    do
        learn I1 -> P said I2 [E]

with PERSON: Principal, ORG: Principal, TRIAL: Trial, R: Record, K: Int, N: Int, E: Evidence
    if
		PERSON said requestToRead(PERSON, R) [E]
    if
		ORG said mayRead(PERSON, R) [_]
	if
		asInfon({|"ct"|
			ppalName(ORG) == org(TRIAL) &&
			R == record(N, TRIAL)
		|})
    if
		asInfon({|"keys"|
			"/keys/key[@record='$R']/@key" | K
		|})
	do
		say with justification to PERSON:
			keyForRecord(K, R)
    forget
        PERSON said requestToRead(PERSON, R) [E]