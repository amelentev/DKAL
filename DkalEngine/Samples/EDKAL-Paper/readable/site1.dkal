#include "common.dkal"

site1 knows
  org(TRIAL) tdonS SITE participates in TRIAL
  org(TRIAL) tdonS SITE is allocated patients N1 to N2 in TRIAL

if site1 knows
   site1 is allocated patients N1 to N2 in TRIAL
   site1 participates in TRIAL
   asInfon(physUnnotified(PHYS,TRIAL))
   asInfon(physPatients(PHYS,TRIAL,P1,P2))
   asInfon(N1 <= P1 && P2 <= N2)
then
   say with justification to PHYS
      PHYS participates in TRIAL at site1 as a physician
      PHYS is allocated patients P1 to P2 in TRIAL at site1
   send with justification to PHYS
      if
         asInfon(P1 <= N && N <= P2)
         asInfon(R == record(N,TRIAL))
      then
         site1 implied PHYS may read R
   send with justification to PHYS
      if
         asInfon(N1 <= N && N <= N2)
         asInfon(R == record(N,TRIAL))
         site1 implied PHYS may read R
      then
         org(TRIAL) implied PHYS may read R
