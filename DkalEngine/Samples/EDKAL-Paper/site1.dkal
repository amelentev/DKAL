#include "common.dkal"

function [2] physUnnotified ( {P:principal}, {T:trial} ) returns {R:bool}
  is physAssignments.phys == P && physAssignments.trial == T && physAssignments.unnotified == R
function [2] physPatients ( {P:principal}, {T:trial}, {N1:int}, {N2:int} ) returns {R:bool}
  is physAssignments.phys == P && physAssignments.trial == T && physAssignments.n1 == N1 && physAssignments.n2 == N2 && R == true

site1 knows
  org(TRIAL) tdonS
     SITE participates in TRIAL
     SITE is allocated patients N1 to N2 in TRIAL

if site1 knows
   site1 is allocated patients N1 to N2 in TRIAL
   asInfon(physUnnotified(PHYS,TRIAL))
   asInfon(physPatients(PHYS,TRIAL,P1,P2))
   asInfon(N1 <= P1 && P2 <= N2)
then
   say with justification to PHYS
      PHYS participates in TRIAL at site1 as a physician
      PHYS is allocated patients P1 to P2 in TRIAL at site1
      if
        asInfon(P1 <= N && N <= P2)
      then
        PHYS may read record(N,TRIAL)
   communicate with justification to PHYS
      if
         asInfon(P1 <= N && N <= P2)
         site1 said PHYS may read record(N,TRIAL)
      then
         org(TRIAL) implied PHYS may read record(N,TRIAL)

