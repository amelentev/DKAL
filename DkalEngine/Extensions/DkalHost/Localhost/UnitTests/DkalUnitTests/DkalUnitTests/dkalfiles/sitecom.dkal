type trial   // trials.id
type patient // patients.id
type record  // records.id
// principal type maps to string

attribute [16] {principal} participates in {trial}
attribute [16] {principal} is assigned id numbers from {int} to {int} in {trial}
attribute [18] {principal} can read {record} in records of trial {trial}
attribute [16] {principal} can read {record} in records of trial {trial} under the authority of {principal}
attribute [16] {principal} is assigned to trial {trial} at {principal}
attribute [16] patient {int} is assigned to {principal} in {trial}
attribute [16] {principal} is a physician assigned to {trial} at {principal}

function [16] {R:record} is a data clarification request in {T:trial} returns {Res:bool}
  is records.id == R && records.trial == T && records.is_clarification == true && Res == true

function [2] patientID ( {R:record} ) returns {P:int}
  is records.id == R && records.patient == P

function [2] croOf ( {T:trial} ) returns {P:principal}
  is trials.id == T && trials.cro == P

function [2] trial0 returns {T:trial}
  is T == trials.id && trials.id == 1

attribute [2] start

#set common_sql  "Server=tcp:v7srhjh9aq.database.windows.net;Database=ftosite;User ID=ecmteam;Password=OrangeJuice100%;Trusted_Connection=False;Encrypt=True;"

#set private_sql "v7srhjh9aq.database.windows.net;Database=ftosite;User ID=ecmteam;Password=OrangeJuice100%;Trusted_Connection=False;Encrypt=True;"
#set me "_site"
#set x "30"
#set y "30"

if _site knows
  asInfon(croOf(T) == C)
  if
    Prov
    _site implied PHYSICIAN can read R in records of trial T under the authority of _site
  then
    C implied PHYSICIAN can read R in records of trial T under the authority of _site
  PHYSICIAN is a physician assigned to T at _site
  patient N is assigned to PHYSICIAN in T
then they send
  to PHYSICIAN with certificate
    if
      Prov
      _site implied PHYSICIAN can read R in records of trial T under the authority of _site
    then
      C implied PHYSICIAN can read R in records of trial T under the authority of _site

  to PHYSICIAN with certificate
    if
      asInfon(R is a data clarification request in T)
      asInfon(patientID(R) == N)
    then
      _site implied PHYSICIAN can read R in records of trial T under the authority of _site

_site knows
  _physician is a physician assigned to trial0 at _site
  patient 10 is assigned to _physician in trial0

